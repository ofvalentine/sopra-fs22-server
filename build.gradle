plugins {
  id 'org.springframework.boot' version '2.6.4'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'jacoco'
  id 'io.freefair.lombok' version '6.4.1'
}

group 'ch.uzh.ifi.hase'
version '1.0.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'
  implementation 'org.springframework.boot:spring-boot-starter-web:2.6.4'
  testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.4'
  runtimeOnly 'com.h2database:h2'
  implementation 'org.springdoc:springdoc-openapi-ui:1.6.6'
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    csv.required = false
  }
}

/**
 * Disable the Gradle task 'jar' to prevent generating a plain JAR, thus allowing only a fat
 * JAR (with all dependencies) to be generated via the task 'bootJar' when deploying the app.
 *
 * Solves a common Heroku bug where Heroku builds the plain JAR instead of the fat JAR, which
 * results in a successful build that crashes when starting the app. (Alternatively, you can
 * provide Heroku the exact filename of the fat JAR via a Procfile.)
 */
jar {
  enabled = false
}